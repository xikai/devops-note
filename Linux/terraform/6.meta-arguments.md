# 资源依赖关系
* 隐式依赖
>Terraform 通过研究插值表达式中使用的资源属性，自动推断一种资源何时依赖于另一种资源,使用此依赖项信息来确定创建不同资源的正确顺序。
```json
resource "aws_instance" "example_a" {
  ami           = ami-xxxxxxx
  instance_type = "t2.micro"
}

resource "aws_eip" "ip" {
  vpc      = true
  instance = aws_instance.example_a.id   //aws_eip.ip 块定义中对的 aws_instance.example_a.id 引用创建了一个隐式依赖关系
}

//Terraform 使用此依赖项信息来确定创建不同资源的正确顺序。为此，它会创建配置定义的所有资源的依赖关系图。在上面的示例中，Terraform 知道必须在弹性 IP 之前创建 EC2 实例。
```

* 显式依赖depends_on
>假设您在 EC2 实例上运行了一个应用程序，该应用程序需要使用特定的 Amazon S3 存储桶。此依赖项在应用程序内部配置，因此对 Terraform 不可见。可用于 depends_on 显式声明依赖项
```json
resource "aws_s3_bucket" "example" { }

resource "aws_instance" "example_c" {
  ami           = data.aws_ami.amazon_linux.id
  instance_type = "t2.micro"

  depends_on = [aws_s3_bucket.example]
}

module "example_sqs_queue" {
  source  = "terraform-aws-modules/sqs/aws"
  version = "3.3.0"

  depends_on = [aws_s3_bucket.example, aws_instance.example_c]
}

```

# [count计数](https://developer.hashicorp.com/terraform/tutorials/configuration-language/count)
>count 参数使用递增计数器将给定资源或模块复制特定次数
```json
//定义变量，每个子网默认2个实例
variable "instances_per_subnet" {
  description = "Number of EC2 instances in each private subnet"
  type        = number
  default     = 2
}


resource "aws_instance" "app" {
  depends_on = [module.vpc]

  //定义app实例的数量（每个子网2个 * vpc子网数）
  count = var.instances_per_subnet * length(module.vpc.private_subnets)

  ami           = data.aws_ami.amazon_linux.id
  instance_type = var.instance_type

  //资源块conut计数中的每个实例都有不同的递增值(count.index)，从0开始
  subnet_id              = module.vpc.private_subnets[count.index % length(module.vpc.private_subnets)]  //使用 count.index 和 模除法将每个实例分配给私有子网
  vpc_security_group_ids = [module.app_security_group.this_security_group_id]

  ## ...
}
```

* 使用count创建的资源为list对象，必须使用index引用
```json
resource "aws_eip" "nat_gateway_eip" {
  count          = var.disable_enable_nat_gateway ? 1 : 0
  tags = {
     Name        = "${var.env}-${var.project_name}-ngw-eip"
     Project     = var.project_name
     Environment = var.env
   }
}

# 创建单个nat网关
resource "aws_nat_gateway" "ngw" {
  count             = var.disable_enable_nat_gateway ? 1 : 0
  allocation_id     = aws_eip.nat_gateway_eip[count.index].id  //使用count创建的资源为list对象，必须使用index引用
  subnet_id         = aws_subnet.public_subnet[0].id
  connectivity_type = "public"
  tags = {
    Name        = "${var.env}-${var.project_name}-ngw"
    Project     = var.project_name
    Environment = var.env
  }
}
```

* 为集合中的项创建带有星号的给定属性的所有值的列表
>可以使用与列表索引相同的表示法引用集合中的单个项目(aws_instance.app[0] 引用第一个实例)
```json
module "elb_http" {
  source  = "terraform-aws-modules/elb/aws"
  version = "3.0.1"

##...

  security_groups = [module.lb_security_group.this_security_group_id]
  subnets         = module.vpc.public_subnets

  number_of_instances = length(aws_instance.app)
  instances           = aws_instance.app.*.id  //实例的所有 ID 的列表

  listener = [{
    instance_port     = "80"
    instance_protocol = "HTTP"
    lb_port           = "80"
    lb_protocol       = "HTTP"
  }]

##...
```

# [for_each](https://developer.hashicorp.com/terraform/language/meta-arguments/for_each)
>for_each接收一个map或set对象，并为该映射或集中的每个项创建一个实例。使用for_each时 map的key（或一组字符串中的所有值）必须是已知值。
  * each.key — 与此实例对应的映射键（或集合成员）
  * each.value — 与此实例对应的映射值（如果提供了集合，则与 each.key相同） 
```json
//map
resource "azurerm_resource_group" "rg" {
  for_each = tomap({
    a_group       = "eastus"
    another_group = "westus2"
  })
  name     = each.key
  location = each.value
}

//Set of strings
resource "aws_iam_user" "the-accounts" {
  for_each = toset(["Todd", "James", "Alice", "Dottie"])
  name     = each.key
}

//Child module: 子模块：
module "bucket" {
  for_each = toset(["assets", "media"])
  source   = "./publish_bucket"
  name     = "${each.key}_bucket"
}

```


# [lifecycle](https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle)
* create_before_destroy
>用于控制资源的创建和销毁顺序。它确保在销毁旧资源之前先创建新资源，以减少服务中断。（如数据库实例或服务器，使用这个属性可以确保新资源完全创建并运行正常后，再销毁旧资源。）
```json
// 假设您有一个 AWS EC2 实例资源，如果您希望在 Terraform 更新资源时先创建新的实例，然后再销毁旧的实例，可以使用 create_before_destroy 属性
resource "aws_instance" "example" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = "t2.micro"
  
  lifecycle {
    create_before_destroy = true
  }
}
```
* ignore_changes
>用于指定在资源配置中哪些属性的变化应该被忽略。这样可以防止 Terraform 在检测到这些属性变化时尝试更新资源。
```json
resource "aws_instance" "example" {
  ami           = "ami-0c55b159cbfafe1f0"
  instance_type = "t2.micro"
  tags = {
    Name = "example-instance"
  }
  
  //在这个示例中，如果实例的标签在 Terraform 管理之外发生了变化（例如，通过 AWS 控制台或 API 修改标签），Terraform 将不会在下次运行 terraform apply 时尝试恢复这些标签到配置中的状态。
  lifecycle {
    ignore_changes = [tags]
  }
}
```

* replace_triggered_by
>当任何引用的项发生更改时替换资源
```
resource "aws_appautoscaling_target" "ecs_target" {
  # ...
  lifecycle {
    replace_triggered_by = [
      # Replace `aws_appautoscaling_target` each time this instance of
      # the `aws_ecs_service` is replaced.
      aws_ecs_service.svc.id
    ]
  }
}
```

* 自定义条件检查
```json
resource "aws_instance" "example" {
  instance_type = "t2.micro"
  ami           = "ami-abc123"

  lifecycle {
    # The AMI ID must refer to an AMI that contains an operating system
    # for the `x86_64` architecture.
    precondition {
      condition     = data.aws_ami.example.architecture == "x86_64"
      error_message = "The selected AMI must be for the x86_64 architecture."
    }
  }
}
```